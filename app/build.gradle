apply plugin: 'com.android.application'

tasks.create(name: "getVersion") {

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {

        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger()
        def version = versionProps['VERSION'].toInteger()
        def major = versionProps['MAJOR'].toInteger()
        def minor = versionProps['MINOR'].toInteger()
        def candidate = versionProps['CANDIDATE'].toString()

        if (candidate.equals('')) {
            project.ext.globalVersion = version.toString() + "." + major.toString() + "." + minor.toString()
        } else {
            project.ext.globalVersion = version.toString() + "." + major.toString() + "." + minor.toString() + "-" + candidate
        }

        project.ext.code = code

    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    preBuild.dependsOn "getVersion"

    defaultConfig {
        applicationId "com.msa.ui"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode project.ext.code
        versionName project.ext.globalVersion
        multiDexEnabled true
    }

    signingConfigs {
        lemonde {
            try {
                keyAlias LEMONDE_KEY_ALLIAS
                keyPassword LEMONDE_KEY_PASSWORD
                storeFile file(LEMONDE_KEYSTORE_FILE)
                storePassword LEMONDE_KEYSTORE_PASSWORD
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    productFlavors {
        lemonde {
            applicationId "com.msa.lemonde.rss"
            versionName project.ext.globalVersion
            signingConfig signingConfigs.lemonde
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
        }
        release {
            debuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.volley:volley:1.0.0'
}
